plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.7.0'
    id 'org.jetbrains.kotlin.kapt' version '1.7.0'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
}

ext {
    dockerImageVersion = release_version
    cradleVersion = '3.2.2-TH2-3789-2508815656-SNAPSHOT'
    ktor_version = '2.0.2'
    kotlin_version = '1.7.0'
}

group 'com.exactpro.th2'
version release_version

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
}

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    compileOnly 'com.google.auto.service:auto-service:1.0.1'
    kapt 'com.google.auto.service:auto-service:1.0.1'

    api platform('com.exactpro.th2:bom:4.0.2')

    implementation 'com.exactpro.th2:cache-common:0.0.1-new-model-4181565217-8cd9481-SNAPSHOT'
    implementation 'com.exactpro.th2:common:3.39.0'
    implementation "com.exactpro.th2:cradle-core:${cradleVersion}"

    implementation "io.ktor:ktor-server-content-negotiation-jvm:$ktor_version"
    implementation "io.ktor:ktor-server-core-jvm:$ktor_version"
    implementation "io.ktor:ktor-serialization-jackson-jvm:$ktor_version"
    implementation "io.ktor:ktor-server-netty-jvm:$ktor_version"
    implementation "ch.qos.logback:logback-classic:1.4.0"
    implementation("io.ktor:ktor-server-compression:$ktor_version")
    implementation "io.ktor:ktor-server-status-pages:$ktor_version"

    implementation 'com.arangodb:arangodb-java-driver:6.17.0'
    implementation 'com.arangodb:jackson-dataformat-velocypack:3.0.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.3'

    implementation 'org.apache.logging.log4j:log4j-core:2.17.2'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.17.2'
    implementation 'io.github.microutils:kotlin-logging:2.1.23'
}

jar {
    manifest {
        attributes(
                'Created-By': "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title': '',
                'Specification-Vendor': 'Exactpro Systems LLC',
                'Implementation-Title': project.archivesBaseName,
                'Implementation-Vendor': 'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version': project.version
        )
    }
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

test {
    useJUnitPlatform()
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

application {
    mainClass.set('com.exactpro.th2.cradle.cache.MainKt')
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
}
